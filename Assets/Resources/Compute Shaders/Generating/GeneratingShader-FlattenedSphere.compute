#pragma kernel Main

struct voxel
{
    int exists;
};

int3 model_size;
int3 chunk_size;
int3 chunk_id;
float voxel_size;

RWStructuredBuffer<voxel> voxels;

int get_voxel_index_in_chunk(int3 id)
{
    if (id.x >= chunk_size.x || id.y >= chunk_size.y || id.z >= chunk_size.z)
    {
        return -1;
    }

    return id.z * chunk_size.x * chunk_size.y + id.y * chunk_size.x + id.x;
}

bool is_in_model(uint3 id)
{
    bool is_valid = chunk_id.x * chunk_size.x + id.x < model_size.x;

    is_valid = is_valid && chunk_id.y * chunk_size.y + id.y < model_size.y;
    is_valid = is_valid && chunk_id.z * chunk_size.z + id.z < model_size.z;
    
    return is_valid;
}

float get_squared(float x)
{
    return x * x;
}

bool is_in_ellipse(uint3 id)
{
    const float3 offset = -voxel_size * model_size / 2 + voxel_size * chunk_size * chunk_id;
    
    const float3 ellipse_coordinates = float3(0, 0, 0);

    const float3 voxel_coordinates = float3(
        id.x * voxel_size + voxel_size / 2.0,
        id.y * voxel_size + voxel_size / 2.0,
        id.z * voxel_size + voxel_size / 2.0) + offset;

    const float a = voxel_size * model_size.x / 2.0;
    const float b = voxel_size * model_size.y / 4.0;
    const float c = voxel_size * model_size.z / 2.0;
    
    return get_squared(voxel_coordinates.x - ellipse_coordinates.x) / (a * a)
        + get_squared(voxel_coordinates.y - ellipse_coordinates.y) / (b * b)
        + get_squared(voxel_coordinates.z - ellipse_coordinates.z) / (c * c) <= 1.0;
}

[numthreads(10, 10, 10)]
void Main (uint3 id : SV_DispatchThreadID)
{
    const int index = get_voxel_index_in_chunk(id);
    
    if (index != -1)
    {
        if (is_in_ellipse(id))
        {
            voxels[index].exists = 1;
        }
        else
        {
            voxels[index].exists = 0;
        }
    }
}